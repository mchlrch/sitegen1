module workflow.SiteGenerator

import org.eclipse.emf.mwe.utils.*

var targetDir = "src-gen"
var fileEncoding = "UTF-8"
var modelPath = "../ch.miranet/model"

Workflow {

	component = org.eclipse.xtext.mwe.Reader {
		// lookup all resources on the classpath
		// useJavaClassPath = true

		// or define search scope explicitly
		path = modelPath

		// this class will be generated by the xtext generator 
		register = ch.miranet.dsl.site.SiteDslStandaloneSetup {}
		load = {
			slot = "elements"
			type = "ModelElement"
			firstOnly = false
		}
	}
	
	component = org.eclipse.xtend.XtendComponent {
		metaModel = org.eclipse.xtend.typesystem.emf.EmfMetaModel {			
			metaModelPackage = "ch.miranet.dsl.site.siteDsl.SiteDslPackage"
		}
		
		metaModel = org.eclipse.xtend.typesystem.emf.EmfMetaModel {			
			metaModelPackage = "ch.miranet.dsl.site.siteDomain.SiteDomainPackage"
		}
		
		metaModel = org.eclipse.xtend.typesystem.emf.EmfMetaModel {
			metaModelPackage = "ch.miranet.dsl.site.siteGenmodel.SiteGenmodelPackage"
		}
		
		
		invoke = "ch::miranet::dsl::site::Dsl2Genmodel::createSite(elements)"
		outputSlot = "site"
	}


	component = org.eclipse.xpand2.Generator {
		expand = "ch::miranet::dsl::site::genmodel::Page::main FOREACH site.pages"
		outlet = {
			path = targetDir
		}
		fileEncoding = fileEncoding
	}
	
	component = org.eclipse.xpand2.Generator {
		expand = "ch::miranet::dsl::site::genmodel::RedirectionPage::main FOREACH site.redirectionPages"
		outlet = {
			path = targetDir
		}
		fileEncoding = fileEncoding
	}
	
	component = org.eclipse.xpand2.Generator {
		expand = "ch::miranet::dsl::site::genmodel::Feed::main(\"http://www.miranet.ch/\") FOR site"
		outlet = {
			path = targetDir
		}
		fileEncoding = fileEncoding
	}
	
	/* ***************************
	 * dieser generator erzeugt nur simple textfiles fürs debugging des models
	 * ***************************/
	 component = org.eclipse.xpand2.Generator {
		expand = "ch::miranet::dsl::site::genmodel::DebugPage::main FOREACH site.pages"
		outlet = {
			path = targetDir
		}
		fileEncoding = fileEncoding
	}
	
	/* ***************************
	 * dieser generator erzeugt nur simple textfiles fürs debugging des models
	 * ***************************/
	 component = org.eclipse.xpand2.Generator {
		expand = "ch::miranet::dsl::site::genmodel::DebugPage::main FOR site.redirectionPages"
		outlet = {
			path = targetDir
		}
		fileEncoding = fileEncoding
	}
	
	/*
	component = org.eclipse.xpand2.Generator {
		expand = "ch::miranet::dsl::site::Main::frontPage FOR elements.first().eContainer()"
		outlet = {
			path = targetDir
		}
		fileEncoding = fileEncoding
	}
	*/
	
}
